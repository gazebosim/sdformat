load(
    "@gz//bazel/skylark:build_defs.bzl",
    "GZ_FEATURES",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "cmake_configure_file",
)

package(
    default_visibility = GZ_VISIBILITY,
    features = GZ_FEATURES,
)

licenses(["notice"])

cmake_configure_file(
    name = "config",
    src = "test_config.hh.in",
    out = "test_config.hh",
    cmakelists = ["CMakeLists.txt"],
    defines = [],
)

cc_library(
    name = "test_utils",
    hdrs = [
        "test_config.hh",
        "test_utils.hh",
    ],
    includes = ["."],
    deps = [
        GZ_ROOT + "utils:utils",
        GZ_ROOT + "sdformat:sdformat",
    ],
)

integration_test_sources = glob(
    ["integration/*.cc"],
    exclude = [
        "integration/schema_test.cc",
        "integration/element_memory_leak.cc",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("integration_", "INTEGRATION_"),
    srcs = [
        src,
        "integration/toml_parser.hh",
    ],
    data = [
        GZ_ROOT + "sdformat:sdf",
        "integration",
        "sdf",
    ],
    env = {
        "GZ_BAZEL": "1",
        "GZ_BAZEL_PATH": "sdformat",
    },
    includes = ["integration"],
    deps = [
        GZ_ROOT + "sdformat:sdformat",
        ":test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in integration_test_sources]

exports_files([
    "sdf",
    "integration",
])
